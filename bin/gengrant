#!/usr/bin/env perl

use strict;
use DBI;

use Act::Config;

# command line arguments
my $username = shift or usage();

# connect to database
my $dbh = DBI->connect(
    $Config->database_dsn,
    $Config->database_user,
    $Config->database_passwd,
);

# fetch database's owner ID
my $sth = $dbh->prepare('SELECT datdba FROM pg_database WHERE datname = ?');
$sth->execute($Config->database_name);
my ($dbowner) = $sth->fetchrow_array();

# fetch list of relations
$sth = $dbh->prepare(
    "SELECT relname, relacl FROM pg_class"
    . " WHERE relowner = ? AND relname NOT LIKE ? AND relkind IN (?, ?) ORDER BY relname"
    
);
$sth->execute($dbowner, 'pg_%', 'r', 'S');
while (my ($relname, $relacl) = $sth->fetchrow_array()) {
    for ($relacl) {   # {eric=arwdRxt/eric,nobody=arwdRxt/eric}
        s/^\{//;
        s/\}$//;
        unless ( { map split('='), split ',' }->{$username} ) {
            print "GRANT ALL ON $relname TO $username;\n";
        }
    }
}
####################
sub usage
{
    die "Syntax: $0 user_name\n";
}
